import groovy.io.FileType

buildscript {
  ext.jenkinsJobdslPluginVersion = '3.7.0'

  repositories {
    mavenLocal()
    maven {
      url 'https://plugins.gradle.org/m2'
    }
    
    maven {
      url 'https://repo.jenkins-ci.org/releases/'
    }
  }

  dependencies {
    classpath( "com.here.gradle.plugins:gradle-jenkins-jobdsl-plugin:$jenkinsJobdslPluginVersion" ) {
      // Exclude unneeded transitive dependencies that can cause dependency resolution issues.
      exclude module: 'bootstrap'
      exclude module: 'groovy-all'
      exclude module: 'org-netbeans-insane'

      // Exclude unneeded transitive dependencies on XML libraries that can break the generation of config files.
      // See: https://issues.jenkins-ci.org/browse/JENKINS-35638
      exclude module: 'xalan'
      exclude module: 'xercesImpl'
    }
  }
}

apply plugin: 'codenarc'
apply plugin: 'com.here.jobdsl'

project.ext {
  jenkinsVersion = '2.190.1'

  groovyVersion = '2.4.15'
  jobDslVersion = '1.76'
}

repositories {
  mavenLocal()
  maven {
    url 'https://plugins.gradle.org/m2'
  }
}

dependencies {
  compile 'com.amazonaws:aws-java-sdk-s3:1.11.594',
          "org.codehaus.groovy:groovy-all:$groovyVersion",
          'com.fasterxml.jackson.core:jackson-databind:2.9.9.1',
          "org.jenkins-ci.plugins:job-dsl:$jobDslVersion@jar",
          'org.jenkins-ci.main:jenkins-test-harness:2.53',
          "org.jenkins-ci.main:jenkins-war:$jenkinsVersion",
          'org.slf4j:slf4j-simple:1.7.25'

  // Uncomment below to trigger usage of Jenkins war bundled plugins
  jenkinsPlugins 'org.jenkins-ci.plugins:scm-api:2.6.3'
  jenkinsPlugins 'org.jenkins-ci.plugins:git:3.12.1'

  testCompile 'org.hamcrest:hamcrest-all:1.3'
}

configurations.all {
  exclude group: 'xalan', module: 'xalan'
  exclude group: 'xerces', module: 'xercesImpl'
  exclude group: 'org.netbeans.modules', module: 'org-netbeans-insane'
}

sourceSets {
  jobdsl {
    groovy {
      srcDirs 'jobdsl'
    }
  }
}

